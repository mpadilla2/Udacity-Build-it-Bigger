apply plugin: 'com.android.application'
apply plugin: 'com.google.cloud.tools.endpoints-framework-client'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.google.cloud.tools:endpoints-framework-gradle-plugin:1.0.2'
    }
}

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        applicationId "com.udacity.gradle.builditbigger"
        minSdkVersion 19
        //noinspection OldTargetApi
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // Resolve "named flavor dimension" error - Reference: https://goo.gl/5Weq2x
    flavorDimensions "tier"

    productFlavors {
        free {
            applicationId "com.udacity.gradle.builditbigger.free"
            dimension "tier"
        }
        paid {
            applicationId "com.udacity.gradle.builditbigger.paid"
            dimension "tier"
        }
    }

    // Reference: https://developer.android.com/studio/build/build-variants#sourcesets
    sourceSets{
        paid{
            manifest.srcFile 'src/paid/AndroidManifest.xml'
        }

        free{
            manifest.srcFile 'src/free/AndroidManifest.xml'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    endpointsServer project(path: ':backend', configuration: 'endpoints')
    implementation 'com.android.support:appcompat-v7:27.1.1'

    // Resolve transitive dependencies conflict.
    // Use https://docs.gradle.org/current/userguide/inspecting_dependencies.html
    // Identified it's originating from com.google.android.gms:play-services-ads:15.0.1 requiring 26.1.0
    // Force use of 27.1.1

    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support:customtabs:27.1.1'
    implementation 'com.android.support:support-media-compat:27.1.1'
    freeImplementation 'com.google.android.gms:play-services-ads:15.0.1'

    // Resolve "conflict with classes now provided by Android" error - Reference: https://developers.google.com/gsuite/guides/android
    implementation('com.google.api-client:google-api-client:1.23.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    implementation('com.google.http-client:google-http-client-android:1.23.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation project(":JavaJokes")
    implementation project(":jokedemo")

    testImplementation 'junit:junit:4.12'
    testImplementation 'com.android.support:support-annotations:27.1.1'


    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support:support-annotations:27.1.1'
    androidTestImplementation 'com.android.support:support-v4:27.1.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-intents:3.0.2'
    androidTestImplementation ('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
}

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

